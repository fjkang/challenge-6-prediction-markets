//SPDX-License-Identifier: MIT
pragma solidity >=0.8.0 <0.9.0;

import "hardhat/console.sol";
import { PredictionMarketToken } from "./PredictionMarketToken.sol";
import { Ownable } from "@openzeppelin/contracts/access/Ownable.sol";

contract PredictionMarket is Ownable {
    /////////////////
    /// Errors //////
    /////////////////

    error PredictionMarket__MustProvideETHForInitialLiquidity();
    error PredictionMarket__InvalidProbability();
    error PredictionMarket__PredictionAlreadyReported();
    error PredictionMarket__OnlyOracleCanReport();
    error PredictionMarket__OwnerCannotCall();
    error PredictionMarket__PredictionNotReported();
    error PredictionMarket__InsufficientWinningTokens();
    error PredictionMarket__AmountMustBeGreaterThanZero();
    error PredictionMarket__MustSendExactETHAmount();
    error PredictionMarket__InsufficientTokenReserve(Outcome _outcome, uint256 _amountToken);
    error PredictionMarket__TokenTransferFailed();
    error PredictionMarket__ETHTransferFailed();
    error PredictionMarket__InsufficientBalance(uint256 _tradingAmount, uint256 _userBalance);
    error PredictionMarket__InsufficientAllowance(uint256 _tradingAmount, uint256 _allowance);
    error PredictionMarket__InsufficientLiquidity();
    error PredictionMarket__InvalidPercentageToLock();

    //////////////////////////
    /// State Variables //////
    //////////////////////////

    enum Outcome {
        YES,
        NO
    }

    uint256 private constant PRECISION = 1e18;

    /// Checkpoint 2 ///
    address public immutable i_oracle;
    uint256 public immutable i_initialTokenValue;
    uint256 public immutable i_initialYesProbability;
    uint256 public immutable i_percentageLocked;

    string public s_question;
    uint256 public s_ethCollateral;
    uint256 public s_lpTradingRevenue;
    /// Checkpoint 3 ///
    PredictionMarketToken public immutable i_yesToken;
    PredictionMarketToken public immutable i_noToken;

    /// Checkpoint 5 ///
    PredictionMarketToken public s_winningToken;
    // âš ï¸è¦è®¾ç½®ä¸ºpublic
    bool public s_isReported;

    /////////////////////////
    /// Events //////
    /////////////////////////

    event TokensPurchased(address indexed buyer, Outcome outcome, uint256 amount, uint256 ethAmount);
    event TokensSold(address indexed seller, Outcome outcome, uint256 amount, uint256 ethAmount);
    event WinningTokensRedeemed(address indexed redeemer, uint256 amount, uint256 ethAmount);
    event MarketReported(address indexed oracle, Outcome winningOutcome, address winningToken);
    event MarketResolved(address indexed resolver, uint256 totalEthToSend);
    event LiquidityAdded(address indexed provider, uint256 ethAmount, uint256 tokensAmount);
    event LiquidityRemoved(address indexed provider, uint256 ethAmount, uint256 tokensAmount);

    /////////////////
    /// Modifiers ///
    /////////////////

    /// Checkpoint 5 ///
    modifier predictionNotReported() {
        if (s_isReported) {
            revert PredictionMarket__PredictionAlreadyReported();
        }
        _;
    }

    /// Checkpoint 6 ///
    modifier predictionAlreadyReported() {
        if (!s_isReported) {
            revert PredictionMarket__PredictionNotReported();
        }
        _;
    }

    /// Checkpoint 8 ///
    modifier notOwner() {
        if (msg.sender == owner()) {
            revert PredictionMarket__OwnerCannotCall();
        }
        _;
    }
    modifier amountGreaterThanZero(uint256 _amount) {
        if (_amount == 0) {
            revert PredictionMarket__AmountMustBeGreaterThanZero();
        }
        _;
    }

    //////////////////
    ////Constructor///
    //////////////////

    constructor(
        address _liquidityProvider,
        address _oracle,
        string memory _question,
        uint256 _initialTokenValue,
        uint8 _initialYesProbability,
        uint8 _percentageToLock
    ) payable Ownable(_liquidityProvider) {
        /// Checkpoint 2 ////
        // 2.1 åˆå§‹åŒ–oracleåœ°å€
        i_oracle = _oracle;
        // 2.2 åˆå§‹åŒ–question
        s_question = _question;
        // 2.3 åˆå§‹åŒ–TokenValueï¼Œéƒ¨ç½²è„šæœ¬ä¸­å€¼=0.01
        i_initialTokenValue = _initialTokenValue;
        // 2.4 åˆå§‹åŒ–s_ethCollateralï¼Œéƒ¨ç½²è„šæœ¬ä¸­å€¼=1
        if (msg.value == 0) {
            revert PredictionMarket__MustProvideETHForInitialLiquidity();
        }
        s_ethCollateral = msg.value;
        // 2.5 åˆå§‹åŒ–YesProbabilityï¼Œå¹¶é™å®šåœ¨0~100ä¹‹é—´
        if (_initialYesProbability == 0 || _initialYesProbability >= 100) {
            revert PredictionMarket__InvalidProbability();
        }
        i_initialYesProbability = _initialYesProbability;
        // 2.6 åˆå§‹åŒ–percentageToLockï¼Œå¹¶é™å®šåœ¨0~100ä¹‹é—´
        if (_percentageToLock == 0 || _percentageToLock >= 100) {
            revert PredictionMarket__InvalidPercentageToLock();
        }
        i_percentageLocked = _percentageToLock;
        /// Checkpoint 3 ////
        // 3.1 è®¡ç®—ç”¨æˆ·åˆå§‹åŒ–æ€»tokenä¸ªæ•°= 1 / 0.01 * 1e18 = 100
        uint256 initialTokenAmount = (msg.value * PRECISION) / _initialTokenValue;
        // 3.2 åˆå§‹åŒ–Yå¸
        i_yesToken = new PredictionMarketToken("Yes", "Y", msg.sender, initialTokenAmount);
        // 3.3 åˆå§‹åŒ–Nå¸
        i_noToken = new PredictionMarketToken("No", "N", msg.sender, initialTokenAmount);
        // 3.4 è®¡ç®—Yå¸å’ŒNå¸çš„é”å®šæ± ,å…¬å¼(yesToken + noToken) * probability/100 * lock/100
        uint256 initialAmountLocked = (initialTokenAmount * 2 * _percentageToLock) / 100;
        uint256 initialYesAmountLocked = (_initialYesProbability * initialAmountLocked) / 100;
        uint256 initialNoAmountLocked = ((100 - _initialYesProbability) * initialAmountLocked) / 100;
        // 3.5 æŠŠé”å®šçš„Yå¸å’ŒNå¸å‘ç”Ÿç»™éƒ¨ç½²çš„äºº
        bool sentY = i_yesToken.transfer(msg.sender, initialYesAmountLocked);
        bool sentN = i_noToken.transfer(msg.sender, initialNoAmountLocked);
        // 3.6 ç¡®ä¿è½¬ç§»æˆåŠŸ
        if (!sentY || !sentN) {
            revert PredictionMarket__TokenTransferFailed();
        }
    }

    /////////////////
    /// Functions ///
    /////////////////

    /**
     * @notice Add liquidity to the prediction market and mint tokens
     * @dev Only the owner can add liquidity and only if the prediction is not reported
     */
    function addLiquidity() external payable onlyOwner predictionNotReported {
        //// Checkpoint 4 ////
        // 1.æ ¡éªŒè¾“å…¥çš„eth
        if (msg.value == 0) {
            revert PredictionMarket__MustSendExactETHAmount();
        }
        // 2.è®¡ç®—è¦mintçš„tokenæ•°é‡
        uint256 addTokenAmount = (msg.value * PRECISION) / i_initialTokenValue;
        // 3.ç»™åˆçº¦é“¸é€ Yå¸å’ŒNå¸
        i_yesToken.mint(address(this), addTokenAmount);
        i_noToken.mint(address(this), addTokenAmount);
        // 4.æ›´æ–°ethçš„æŠµæŠ¼æ€»é‡
        s_ethCollateral += msg.value;

        // 5.å‘é€æµåŠ¨æ€§å¢åŠ äº‹ä»¶
        emit LiquidityAdded(msg.sender, msg.value, addTokenAmount);
    }

    /**
     * @notice Remove liquidity from the prediction market and burn respective tokens, if you remove liquidity before prediction ends you got no share of lpReserve
     * @dev Only the owner can remove liquidity and only if the prediction is not reported
     * @param _ethToWithdraw Amount of ETH to withdraw from liquidity pool
     */
    function removeLiquidity(uint256 _ethToWithdraw) external onlyOwner predictionNotReported {
        //// Checkpoint 4 ////
        // 1.è®¡ç®—å–å›ethå¯¹åº”é”€æ¯çš„tokenæ•°é‡
        uint256 burnTokensAmount = (_ethToWithdraw * PRECISION) / i_initialTokenValue;
        // 2.æ ¡éªŒæ˜¯å¦æœ‰è¶³å¤Ÿçš„Yå¸å’ŒNå¸è¿›è¡Œé”€æ¯
        if (burnTokensAmount > i_yesToken.balanceOf(address(this))) {
            revert PredictionMarket__InsufficientTokenReserve(Outcome.YES, burnTokensAmount);
        }
        if (burnTokensAmount > i_noToken.balanceOf(address(this))) {
            revert PredictionMarket__InsufficientTokenReserve(Outcome.NO, burnTokensAmount);
        }
        // 3.é”€æ¯Yå¸å’ŒNå¸æ•°é‡
        i_yesToken.burn(address(this), burnTokensAmount);
        i_noToken.burn(address(this), burnTokensAmount);
        // 4.è½¬è´¦ç»™owner
        (bool sent, ) = msg.sender.call{ value: _ethToWithdraw }("");
        if (!sent) {
            revert PredictionMarket__ETHTransferFailed();
        }
        // 5.æ›´æ–°ethæŠµæŠ¼æ€»é‡
        s_ethCollateral -= _ethToWithdraw;

        // 6.å‘ç”ŸæµåŠ¨æ€§å‡å°‘äº‹ä»¶
        emit LiquidityRemoved(msg.sender, _ethToWithdraw, burnTokensAmount);
    }

    /**
     * @notice Report the winning outcome for the prediction
     * @dev Only the oracle can report the winning outcome and only if the prediction is not reported
     * @param _winningOutcome The winning outcome (YES or NO)
     */
    function report(Outcome _winningOutcome) external predictionNotReported {
        //// Checkpoint 5 ////
        // 1.åˆ¤æ–­æ‰§è¡Œè€…æ˜¯å¦æ˜¯oracle
        if (msg.sender != i_oracle) {
            revert PredictionMarket__OnlyOracleCanReport();
        }
        // 2.è®¾ç½®è·èƒœç»“æœ
        s_winningToken = _winningOutcome == Outcome.YES ? i_yesToken : i_noToken;
        // 3.å°†s_isReportedç½®ä¸ºtrue
        s_isReported = true;

        // 4.è§¦å‘reportäº‹ä»¶
        emit MarketReported(msg.sender, _winningOutcome, address(s_winningToken));
    }

    /**
     * @notice Owner of contract can redeem winning tokens held by the contract after prediction is resolved and get ETH from the contract including LP revenue and collateral back
     * @dev Only callable by the owner and only if the prediction is resolved
     * @return ethRedeemed The amount of ETH redeemed
     */
    function resolveMarketAndWithdraw() external onlyOwner predictionAlreadyReported returns (uint256 ethRedeemed) {
        /// Checkpoint 6 ////
        // 1.æ ¡éªŒè·èƒœçš„tokenæ•°é‡
        uint256 winningTokensAmount = s_winningToken.balanceOf(address(this));
        if (winningTokensAmount > 0) {
            // 2.è®¡ç®—ethRedeemed = è·èƒœçš„tokenæ•°(1e18) * tokenå•ä»·(1e18) / 1e18
            // âš ï¸1e18çš„ç²¾åº¦å¤„ç†
            ethRedeemed = (winningTokensAmount * i_initialTokenValue) / PRECISION;
            // 2.1 é™åˆ¶ethRedeemedä¸èƒ½å¤§äºæŠµæŠ¼çš„ethæ•°
            ethRedeemed = ethRedeemed > s_ethCollateral ? s_ethCollateral : ethRedeemed;
            // 2.2 æ›´æ–°ownerçš„ethæŠµæŠ¼
            s_ethCollateral -= ethRedeemed;
        }
        // 3.è®¡ç®—ownerè·å–ethçš„æ€»æ•° = ethRedeemed + s_lpTradingRevenue
        uint256 totalEthtoGet = ethRedeemed + s_lpTradingRevenue;
        // 4.è·å–å‰å°†tokené”€æ¯
        s_winningToken.burn(address(this), winningTokensAmount);
        // 5.è·å–eth
        (bool sent, ) = msg.sender.call{ value: totalEthtoGet }("");
        if (!sent) {
            revert PredictionMarket__ETHTransferFailed();
        }

        // 6.å‘é€WinningTokensRedeemedäº‹ä»¶
        emit MarketResolved(msg.sender, totalEthtoGet);

        return ethRedeemed;
    }

    /**
     * @notice Buy prediction outcome tokens with ETH, need to call priceInETH function first to get right amount of tokens to buy
     * @param _outcome The possible outcome (YES or NO) to buy tokens for
     * @param _amountTokenToBuy Amount of tokens to purchase
     */
    function buyTokensWithETH(
        Outcome _outcome,
        uint256 _amountTokenToBuy
    ) external payable amountGreaterThanZero(_amountTokenToBuy) predictionNotReported notOwner {
        /// Checkpoint 8 ////
        // 1.è®¡ç®—è´­ä¹°tokenæ‰€éœ€è¦çš„eth
        uint256 ethForBuy = getBuyPriceInEth(_outcome, _amountTokenToBuy);
        // 2.æ ¡éªŒç”¨æˆ·å‘é€ethæ˜¯å¦ç¬¦åˆ
        if (msg.value != ethForBuy) {
            revert PredictionMarket__MustSendExactETHAmount();
        }
        // 3.åˆ¤æ–­ç”¨æˆ·è´­ä¹°çš„å¸ç§
        PredictionMarketToken tokenType = _outcome == Outcome.YES ? i_yesToken : i_noToken;
        // 4.åˆ¤æ–­å½“å‰å¸å‰©ä½™çš„tokenæ˜¯å¦è¶³å¤Ÿ
        uint256 tokenReserves = tokenType.balanceOf(address(this));
        if (_amountTokenToBuy > tokenReserves) {
            revert PredictionMarket__InsufficientTokenReserve(_outcome, _amountTokenToBuy);
        }
        // 5.æµåŠ¨æ€§æ± å¢åŠ 
        s_lpTradingRevenue += ethForBuy;
        // 6.tokenè½¬è´¦
        bool sent = tokenType.transfer(msg.sender, _amountTokenToBuy);
        if (!sent) {
            revert PredictionMarket__TokenTransferFailed();
        }

        // 7.å‘é€TokensPurchasedäº‹ä»¶
        emit TokensPurchased(msg.sender, _outcome, _amountTokenToBuy, ethForBuy);
    }

    /**
     * @notice Sell prediction outcome tokens for ETH, need to call priceInETH function first to get right amount of tokens to buy
     * @param _outcome The possible outcome (YES or NO) to sell tokens for
     * @param _tradingAmount The amount of tokens to sell
     */
    function sellTokensForEth(
        Outcome _outcome,
        uint256 _tradingAmount
    ) external amountGreaterThanZero(_tradingAmount) predictionNotReported notOwner {
        /// Checkpoint 8 ////
        // 1.è·å–äº¤æ˜“çš„å¸ç§
        PredictionMarketToken tokenType = _outcome == Outcome.YES ? i_yesToken : i_noToken;
        // 2.åˆ¤æ–­ç”¨æˆ·æ˜¯å¦æœ‰è¶³å¤Ÿtokenå‡ºå”®
        uint256 userBalance = tokenType.balanceOf(msg.sender);
        if (_tradingAmount > userBalance) {
            revert PredictionMarket__InsufficientBalance(_tradingAmount, userBalance);
        }
        // 3.åˆ¤æ–­æ˜¯å¦æœ‰è¶³å¤Ÿçš„tokenæˆæƒäº¤æ˜“
        uint256 allowance = tokenType.allowance(msg.sender, address(this));
        if (_tradingAmount > allowance) {
            revert PredictionMarket__InsufficientAllowance(_tradingAmount, allowance);
        }
        // 4.è®¡ç®—tokenå¯¹åº”çš„ä»·æ ¼
        uint256 ethForSell = getSellPriceInEth(_outcome, _tradingAmount);
        // 5.æµåŠ¨æ€§æ± æ‰£é™¤
        s_lpTradingRevenue -= ethForSell;
        // 6.tokenè½¬è´¦åˆ°åˆçº¦
        bool sentToken = tokenType.transferFrom(msg.sender, address(this), _tradingAmount);
        if (!sentToken) {
            revert PredictionMarket__TokenTransferFailed();
        }
        // 7.ethè½¬è´¦ç»™ç”¨æˆ·
        (bool sentEth, ) = msg.sender.call{ value: ethForSell }("");
        if (!sentEth) {
            revert PredictionMarket__ETHTransferFailed();
        }

        // 8.å‘é€TokensSoldäº‹ä»¶
        emit TokensSold(msg.sender, _outcome, _tradingAmount, ethForSell);
    }

    /**
     * @notice Redeem winning tokens for ETH after prediction is resolved, winning tokens are burned and user receives ETH
     * @dev Only if the prediction is resolved
     * @param _amount The amount of winning tokens to redeem
     */
    function redeemWinningTokens(uint256 _amount) external {
        /// Checkpoint 9 ////
    }

    /**
     * @notice Calculate the total ETH price for buying tokens
     * @param _outcome The possible outcome (YES or NO) to buy tokens for
     * @param _tradingAmount The amount of tokens to buy
     * @return The total ETH price
     */
    function getBuyPriceInEth(Outcome _outcome, uint256 _tradingAmount) public view returns (uint256) {
        /// Checkpoint 7 ////
        // è¿”å›ä¹°å…¥ä»·æ ¼
        return _calculatePriceInEth(_outcome, _tradingAmount, false);
    }

    /**
     * @notice Calculate the total ETH price for selling tokens
     * @param _outcome The possible outcome (YES or NO) to sell tokens for
     * @param _tradingAmount The amount of tokens to sell
     * @return The total ETH price
     */
    function getSellPriceInEth(Outcome _outcome, uint256 _tradingAmount) public view returns (uint256) {
        /// Checkpoint 7 ////
        // è¿”å›å‡ºå”®ä»·æ ¼
        return _calculatePriceInEth(_outcome, _tradingAmount, true);
    }

    /////////////////////////
    /// Helper Functions ///
    ////////////////////////

    /**
     * @dev Internal helper to calculate ETH price for both buying and selling
     * @param _outcome The possible outcome (YES or NO)
     * @param _tradingAmount The amount of tokens
     * @param _isSelling Whether this is a sell calculation
     */
    function _calculatePriceInEth(
        Outcome _outcome,
        uint256 _tradingAmount,
        bool _isSelling
    ) private view returns (uint256) {
        /// Checkpoint 7 ////
        // ä»·æ ¼å…¬å¼ï¼šprice = initialTokenValue * probabilityAvg * tradingAmount
        // ğŸŒ°ä¾‹å­
        // probabilityBefore = 50%
        // probabilityAfter = (60 + 10 ) / (10 + 10 + 60 + 0) = 87.5%
        // probAvg = (probBefore + probAfter) / 2 = 68.75%
        // price = 0.01 ETH * 68.75% * 60 = 0.4125 ETH
        // 1.è·å–ç›®å‰å¸çš„å­˜é‡
        (uint256 currentTokenReserves, uint256 otherTokenReserves) = _getCurrentReserves(_outcome);
        // 2.å¦‚æœæ˜¯è´­ä¹°ï¼Œä¿è¯ä¸è¶…å–
        if (!_isSelling && currentTokenReserves < _tradingAmount) {
            revert PredictionMarket__InsufficientLiquidity();
        }
        // 3.è®¡ç®—äº¤æ˜“å‰ç›¸å…³æ•°æ®
        // 3.1 æ€»çš„tokenå­˜é‡
        uint256 totalSupplyToken = i_yesToken.totalSupply();
        // 3.2 äº¤æ˜“å‰å½“å‰tokenå·²å”®
        uint256 currentTokenSoldBefore = totalSupplyToken - currentTokenReserves;
        // 3.3 äº¤æ˜“å‰çš„å¦ä¸€ç§tokenå·²å”®
        uint256 otherTokenSoldBefore = totalSupplyToken - otherTokenReserves;
        // 3.4 äº¤æ˜“å‰tokenæ€»å·²å”®
        uint256 totalTokenSoldBefore = currentTokenSoldBefore + otherTokenSoldBefore;
        // 3.5 è®¡ç®—äº¤æ˜“å‰çš„å¯èƒ½æ€§
        uint256 probabilityBefore = _calculateProbability(currentTokenSoldBefore, totalTokenSoldBefore);
        // 4.è®¡ç®—äº¤æ˜“åç›¸å…³æ•°æ®
        // 4.1 è®¡ç®—å½“å‰tokenäº¤æ˜“åçš„å·²å”®
        uint256 currentTokenSoldAfter = _isSelling
            ? currentTokenSoldBefore - _tradingAmount
            : currentTokenSoldBefore + _tradingAmount;
        // 4.2 äº¤æ˜“åtokenæ€»å·²å”®
        uint256 totalTokenSoldAfter = currentTokenSoldAfter + otherTokenSoldBefore;
        // 4.3 è®¡ç®—äº¤æ˜“åçš„å¯èƒ½æ€§
        uint256 probabilityAfter = _calculateProbability(currentTokenSoldAfter, totalTokenSoldAfter);
        // 5.è®¡ç®—å¹³å‡å¯èƒ½æ€§
        uint256 probabilityAvg = (probabilityBefore + probabilityAfter) / 2;
        // 6.è¿”å›ä»·æ ¼
        // i_initialTokenValue->1e18    probabilityAvg->1e18   _tradingAmount->1e18
        return (i_initialTokenValue * probabilityAvg * _tradingAmount) / PRECISION ** 2;
    }

    /**
     * @dev Internal helper to get the current reserves of the tokens
     * @param _outcome The possible outcome (YES or NO)
     * @return The current reserves of the tokens
     */
    function _getCurrentReserves(Outcome _outcome) private view returns (uint256, uint256) {
        /// Checkpoint 7 ////
        // 1.ç¡®å®šè¦æŸ¥è¯¢çš„å¸æ˜¯Yè¿˜æ˜¯N
        (PredictionMarketToken oneToken, PredictionMarketToken otherToken) = _outcome == Outcome.YES
            ? (i_yesToken, i_noToken)
            : (i_noToken, i_yesToken);
        // 2.è¿”å›å¸çš„æ•°é‡
        return (oneToken.balanceOf(address(this)), otherToken.balanceOf(address(this)));
    }

    /**
     * @dev Internal helper to calculate the probability of the tokens
     * @param tokensSold The number of tokens sold
     * @param totalSold The total number of tokens sold
     * @return The probability of the tokens
     */
    function _calculateProbability(uint256 tokensSold, uint256 totalSold) private pure returns (uint256) {
        /// Checkpoint 7 ////
        // 1.ç›´æ¥è¿”å›å¯èƒ½æ€§ï¼Œâš ï¸* 1e18ä»¥é˜²ç²¾åº¦ä¸¢å¤±
        return (tokensSold * PRECISION) / totalSold;
    }

    /////////////////////////
    /// Getter Functions ///
    ////////////////////////

    /**
     * @notice Get the prediction details
     */
    function getPrediction()
        external
        view
        returns (
            string memory question,
            string memory outcome1,
            string memory outcome2,
            address oracle,
            uint256 initialTokenValue,
            uint256 yesTokenReserve,
            uint256 noTokenReserve,
            bool isReported,
            address yesToken,
            address noToken,
            address winningToken,
            uint256 ethCollateral,
            uint256 lpTradingRevenue,
            address predictionMarketOwner,
            uint256 initialProbability,
            uint256 percentageLocked
        )
    {
        /// Checkpoint 3 ////
        oracle = i_oracle;
        initialTokenValue = i_initialTokenValue;
        percentageLocked = i_percentageLocked;
        initialProbability = i_initialYesProbability;
        question = s_question;
        ethCollateral = s_ethCollateral;
        lpTradingRevenue = s_lpTradingRevenue;
        predictionMarketOwner = owner();
        yesToken = address(i_yesToken);
        noToken = address(i_noToken);
        outcome1 = i_yesToken.name();
        outcome2 = i_noToken.name();
        yesTokenReserve = i_yesToken.balanceOf(address(this));
        noTokenReserve = i_noToken.balanceOf(address(this));
        /// Checkpoint 5 ////
        isReported = s_isReported;
        winningToken = address(s_winningToken);
    }
}
